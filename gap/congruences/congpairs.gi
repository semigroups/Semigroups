############################################################################
##
##  congruences/congpairs.gi
##  Copyright (C) 2015-2021                               Michael C. Young
##
##  Licensing information can be found in the README file of this package.
##
#############################################################################
##
## This file contains functions for any congruence of a semigroup with
## generating pairs.
#############################################################################

InstallImmediateMethod(GeneratingPairsOfAnyCongruence,
                       IsCongruenceCategory
                         and HasGeneratingPairsOfMagmaCongruence,
                       0,
                       GeneratingPairsOfMagmaCongruence);

InstallImmediateMethod(GeneratingPairsOfAnyCongruence,
                       IsLeftCongruenceCategory
                         and HasGeneratingPairsOfLeftMagmaCongruence,
                       0,
                       GeneratingPairsOfLeftMagmaCongruence);

InstallImmediateMethod(GeneratingPairsOfAnyCongruence,
                       IsRightCongruenceCategory
                         and HasGeneratingPairsOfRightMagmaCongruence,
                       0,
                       GeneratingPairsOfRightMagmaCongruence);

#############################################################################
# Constructor
#############################################################################

InstallMethod(_AnyCongruenceByGeneratingPairs,
[IsSemigroup, IsHomogeneousList, IsFunction],
function(S, pairs, filt)
  local fam, C, pair;

  for pair in pairs do
    if not IsList(pair) or Length(pair) <> 2 then
      Error("the 2nd argument <pairs> must consist of lists of ",
            "length 2");
    elif not pair[1] in S or not pair[2] in S then
      Error("the 2nd argument <pairs> must consist of lists of ",
            "elements of the 1st argument <S> (a semigroup)");
    fi;
  od;

  # Create the Object
  fam := GeneralMappingsFamily(ElementsFamily(FamilyObj(S)),
                               ElementsFamily(FamilyObj(S)));

  C := Objectify(NewType(fam, filt and IsAttributeStoringRep),
                 rec());
  SetSource(C, S);
  SetRange(C, S);
  if IsCongruenceCategory(C) then
    SetGeneratingPairsOfMagmaCongruence(C, pairs);
  elif IsLeftCongruenceCategory(C) then
    SetGeneratingPairsOfLeftMagmaCongruence(C, pairs);
  elif IsRightCongruenceCategory(C) then
    SetGeneratingPairsOfRightMagmaCongruence(C, pairs);
  else
    Error("Something has gone wrong, should not have ",
          "been able to reach here!");
  fi;
  return C;
end);

InstallMethod(AsSemigroupCongruenceByGeneratingPairs,
"for semigroup congruence",
[IsSemigroupCongruence],
function(C)
  local S, pairs;
  S := Range(C);
  pairs := GeneratingPairsOfMagmaCongruence(C);
  return SemigroupCongruenceByGeneratingPairs(S, pairs);
end);

InstallMethod(AsRightSemigroupCongruenceByGeneratingPairs,
"for a right semigroup congruence",
[IsRightSemigroupCongruence],
function(C)
  local S, pairs;
  S := Range(C);
  pairs := GeneratingPairsOfRightMagmaCongruence(C);
  return RightSemigroupCongruenceByGeneratingPairs(S, pairs);
end);

InstallMethod(AsLeftSemigroupCongruenceByGeneratingPairs,
"for a left semigroup congruence",
[IsLeftSemigroupCongruence],
function(C)
  local S, pairs;
  S := Range(C);
  pairs := GeneratingPairsOfLeftMagmaCongruence(C);
  return LeftSemigroupCongruenceByGeneratingPairs(S, pairs);
end);

#############################################################################
# Properties of congruences
#############################################################################

InstallMethod(IsRightSemigroupCongruence,
"for a left semigroup congruence with known generating pairs",
[IsLeftSemigroupCongruence and HasGeneratingPairsOfLeftMagmaCongruence],
function(congl)
  local pairs, cong2;
  # Is this left congruence right-compatible?
  # First, create the 2-sided congruence generated by these pairs.
  pairs := GeneratingPairsOfLeftSemigroupCongruence(congl);
  cong2 := SemigroupCongruence(Range(congl), pairs);

  # congl is right-compatible iff these describe the same relation
  if congl = cong2 then
    SetGeneratingPairsOfMagmaCongruence(congl, pairs);
    SetIsSemigroupCongruence(congl, true);
    return true;
  else
    SetIsSemigroupCongruence(congl, false);
    return false;
  fi;
end);

InstallMethod(IsLeftSemigroupCongruence,
"for a right semigroup congruence with known generating pairs",
[IsRightSemigroupCongruence and HasGeneratingPairsOfRightMagmaCongruence],
function(congr)
  local pairs, cong2;
  # Is this right congruence left-compatible?
  # First, create the 2-sided congruence generated by these pairs.
  pairs := GeneratingPairsOfRightSemigroupCongruence(congr);
  cong2 := SemigroupCongruence(Range(congr), pairs);

  # congr is left-compatible iff these describe the same relation
  if congr = cong2 then
    SetGeneratingPairsOfMagmaCongruence(congr, pairs);
    SetIsSemigroupCongruence(congr, true);
    return true;
  else
    SetIsSemigroupCongruence(congr, false);
    return false;
  fi;
end);

InstallMethod(IsSemigroupCongruence,
"for a left semigroup congruence with known generating pairs",
[IsLeftSemigroupCongruence and HasGeneratingPairsOfLeftMagmaCongruence],
IsRightSemigroupCongruence);

InstallMethod(IsSemigroupCongruence,
"for a right semigroup congruence with known generating pairs",
[IsRightSemigroupCongruence and HasGeneratingPairsOfRightMagmaCongruence],
IsLeftSemigroupCongruence);

#############################################################################
# Printing and viewing of congruences
#############################################################################

InstallMethod(PrintObj,
"for IsAnyCongruenceCategory with known generating pairs",
[IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence],
function(C)
  local string;
  if not IsCongruenceCategory(C) then
    string := ShallowCopy(AnyCongruenceString(C));
    string[1] := UppercaseChar(string[1]);
  else
    string := "";
  fi;

  Print(string, "SemigroupCongruence( ");
  PrintObj(Range(C));
  Print(", ");
  Print(GeneratingPairsOfAnyCongruence(C));
  Print(" )");
end);

InstallMethod(ViewObj,
"for IsAnyCongruenceCategory with known generating pairs",
[IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence],
function(C)
  local string;
  if not IsCongruenceCategory(C) then
    string := ShallowCopy(AnyCongruenceString(C));
    Append(string, " ");
  else
    string := "";
  fi;
  Print("<", string, "semigroup congruence over ");
  ViewObj(Range(C));
  Print(" with ",
        Size(GeneratingPairsOfAnyCongruence(C)),
        " generating pairs>");
end);

########################################################################
# Comparison operators
########################################################################

InstallMethod(\=,
"for IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence",
[IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence,
 IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence],
function(lhop, rhop)
  if AnyCongruenceCategory(lhop) = AnyCongruenceCategory(rhop) then
    return Range(lhop) = Range(rhop)
           and ForAll(GeneratingPairsOfAnyCongruence(lhop), x -> x in rhop)
           and ForAll(GeneratingPairsOfAnyCongruence(rhop), x -> x in lhop);
  fi;
  TryNextMethod();
end);

InstallMethod(IsSubrelation,
"for IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence",
[IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence,
 IsAnyCongruenceCategory and HasGeneratingPairsOfAnyCongruence],
function(lhop, rhop)
  # Only valid for certain combinations of types
  if AnyCongruenceCategory(lhop) <> AnyCongruenceCategory(rhop)
      and AnyCongruenceCategory(lhop) <> IsCongruenceCategory then
    TryNextMethod();
  elif Range(lhop) <> Range(rhop) then
    Error("the 1st and 2nd arguments are congruences over different",
          " semigroups");
  fi;

  # Test whether lhop contains all the pairs in rhop
  return ForAll(GeneratingPairsOfAnyCongruence(rhop),
                x -> CongruenceTestMembershipNC(lhop, x[1], x[2]));
end);

########################################################################
# Algebraic operators
########################################################################

BindGlobal("_JoinAnyCongruences",
function(lhop, rhop)
  local Constructor, pairs;
  if Range(lhop) <> Range(rhop) then
    Error("cannot form the join of congruences over different semigroups");
  elif lhop = rhop then
    return lhop;
  fi;

  if IsCongruenceCategory(lhop) then
    Constructor := SemigroupCongruenceByGeneratingPairs;
  elif IsLeftCongruenceCategory(lhop) then
    Constructor := LeftSemigroupCongruenceByGeneratingPairs;
  else
    Assert(1, IsRightCongruenceCategory(lhop));
    Constructor := RightSemigroupCongruenceByGeneratingPairs;
  fi;
  pairs := Concatenation(GeneratingPairsOfAnyCongruence(lhop),
                         GeneratingPairsOfAnyCongruence(rhop));
  return Constructor(Range(lhop), pairs);
end);

InstallMethod(JoinSemigroupCongruences,
"for a semigroup congruence with known generating pairs",
[IsCongruenceCategory and HasGeneratingPairsOfMagmaCongruence,
 IsCongruenceCategory and HasGeneratingPairsOfMagmaCongruence],
_JoinAnyCongruences);

InstallMethod(JoinLeftSemigroupCongruences,
"for a semigroup left congruence with known generating pairs",
[IsLeftCongruenceCategory and HasGeneratingPairsOfLeftMagmaCongruence,
 IsLeftCongruenceCategory and HasGeneratingPairsOfLeftMagmaCongruence],
_JoinAnyCongruences);

InstallMethod(JoinRightSemigroupCongruences,
"for a semigroup right congruence with known generating pairs",
[IsRightCongruenceCategory and HasGeneratingPairsOfRightMagmaCongruence,
 IsRightCongruenceCategory and HasGeneratingPairsOfRightMagmaCongruence],
_JoinAnyCongruences);

MakeReadWriteGlobal("_JoinAnyCongruences");
Unbind(_JoinAnyCongruences);
